" keep buffers open in bg
set hidden

" relative numbering
set relativenumber

" modify keybindings
let mapleader = ","

" use <C-Space> for Vim's keyword autocomplete
inoremap <Nul> <C-n>
map! <C-q> <ESC>

" easy save
noremap <Leader>w :update<CR>
nnoremap <Leader><Leader> <C-^>

" fzf
noremap <C-p> :Files<CR>
noremap <Leader><C-p> :Buf<CR>

" project-wide search
noremap <Leader>a :Ack '

" Test mappings
nmap <silent> t<C-n> :TestNearest<CR>
nmap <silent> t<C-f> :TestFile<CR>
nmap <silent> t<C-s> :TestSuite<CR>
nmap <silent> t<C-l> :TestLast<CR>
nmap <silent> t<C-g> :TestVisit<CR>

" toggle fullscreen pane
nnoremap <C-W>z :call MaximizeToggle()<CR>

function! MaximizeToggle()
  if exists("s:maximize_session")
    exec "source " . s:maximize_session
    call delete(s:maximize_session)
    unlet s:maximize_session
    let &hidden=s:maximize_hidden_save
    unlet s:maximize_hidden_save
  else
    let s:maximize_hidden_save = &hidden
    let s:maximize_session = tempname()
    set hidden
    exec "mksession! " . s:maximize_session
    only
  endif
endfunction

" toggles the quickfix window.
command -bang -nargs=? QFix call QFixToggle(<bang>0)
function! QFixToggle(forced)
  if exists("g:qfix_win") && a:forced == 0
    cclose
  else
    copen
  endif
endfunction

" used to track the quickfix window
augroup QFixToggle
  autocmd!
  autocmd BufWinEnter quickfix let g:qfix_win = bufnr("$")
  autocmd BufWinLeave * if exists("g:qfix_win") && expand("<abuf>") == g:qfix_win | unlet! g:qfix_win | endif
augroup END

nnoremap <Leader><space> :QFix<CR>

" use system clipboard (for yank, etc)
set clipboard=unnamed

"change column highlight color
hi ColorColumn ctermbg=0
hi ColorColumn ctermbg=8
hi Visual ctermbg=8

" tidalvim
let g:tidal_default_config = {"socket_name": "default", "target_pane": "tidal:1.2"}
